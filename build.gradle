buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    ext.euklid_version = 'd5b4c53ccd'
    ext.react_version = 'afb412c977'

    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url 'https://jitpack.io' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    /**
     * Define a function here that should be called by all sub-projects that want to be
     * published.
     * The problem with configuring all here was that the sourcesJar was always resolved
     * in the context of this root project.
     */
    ext.configurePublishing = { sourcesJar ->
        apply plugin: 'maven-publish'

        publishing {
            publications {
                myPublication(MavenPublication) {
                    from components.java
                    groupId 'com.github.cdietze.klay'
                    artifactId project.name
                    version project.version
                    artifact sourcesJar
                }
            }
        }
    }
}

apply plugin: 'kotlin-platform-common'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
    compile "com.github.cdietze.euklid:euklid:$euklid_version"
    compile "com.github.cdietze.react-kt:react-kt:$react_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
}

task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

configurePublishing(sourcesJar)
